// Stuff

/** 2048 game manager. */
class TwosGame {
   // Game board
   field String titleString;
   field String instructionString;
   field String startString;
   field Board board;
   field boolean loseCondition, winCondition;
   field int winThreshold;
   field int nrows, ncols;

   constructor TwosGame new() {
      let titleString = "= = = 2 0 4 8 = = =";
      let instructionString = "Instructions: arrows to move, r to reset, q to quit";
      let startString = "Press spacebar to start...";
      
      // Print main screen
      do Output.moveCursor(1, 22);
      do Output.printString(titleString);
      do Output.moveCursor(3, 7);
      do Output.printString(instructionString);

      let nrows = 4;
      let ncols = 4;
      do init();

      return this;
   }

   method void init() {
      let loseCondition = false;
      let winCondition = false;
      let winThreshold = 16 - 1;
      let board = Board.new(nrows, ncols);
      return;
   }

   /** Main game.
       Has two phases: start screen and game loop.
       Start screen also helps to seed the PRNG. */
   method void run() {
      var Board board_new, board_old, board_alt;
      var char key;
      var boolean started, exit;
      var int initSeed;
      var boolean filled, sameAsBefore;
      var int dir;

      let started = false;
      let exit = false;
      let initSeed = 0;

      // Start screen
      while (~started) {
         do Output.moveCursor(11, 19);
         do Output.printString(startString);

         // Wait for key to be pressed
         while (key = 0) {
            let key = Keyboard.keyPressed();
            let initSeed = initSeed + 1;
         }

         if (key = 32) {  // spacebar
            do Random.setSeed(initSeed);
            let started = true;
         }

         // Wait for key to be released
         while (~(key = 0)) {
            let key = Keyboard.keyPressed();
         }
      }

      do board.initialize();

      // Game loop
      // TODO: have board only populate when blocks move
      // TODO: check edge cases (there was a bug with full board)
      // TODO: add win condition and lose condition... lol
      while (~exit) {
         // Wait for key to be pressed
         while (key = 0) {
            let key = Keyboard.keyPressed();
            // other actions while key is not pressed
         }

         if (key = 81) { let exit = true; }  // q key
         if (key = 82) {  // r key
            do board.dispose();
            do init();
            do board.initialize();
         }
         if ((key > 129) & (key < 134)) {  // arrows
            let board_new = BoardOps.alter(board, key - 129);

            let board_old = board;
            let board = board_new;

            do board.print();

            // check win condition
            do checkWinCondition();


            do Sys.wait(50);
            let filled = board.populateRNG();

            if (~filled) {
               do checkLoseCondition();
            }

            // print status according to condition
            if (winCondition) {
               do Output.moveCursor(13, 24);
               do Output.printString("Y O U   W I N ! !");
            }
            else {
               if (loseCondition) {
                  do Output.moveCursor(13, 23);
                  do Output.printString("G A M E   O V E R");
               }
               else {
                  do board.print();
               }
            }

            do board_old.dispose();
         }

         // Wait for key to be released
         while (~(key = 0)) {
            let key = Keyboard.keyPressed();
         }
      }

      return;
   }

   method void checkWinCondition() {
      let winCondition = board.containsBiggerThan(winThreshold);
      return;
   }

   method void checkLoseCondition() {
      var int dir;
      var Board board_alt;

      let loseCondition = true;
      let dir = 1;
      while (dir < 3) {
         let board_alt = BoardOps.alter(board, dir);
         if (~(board.sameAs(board_alt))) {
            do Output.printInt(dir);
            let loseCondition = false;
            let dir = 3;
         }
         do board_alt.dispose();
         let dir = dir + 1;
      }
      return;
   }

   method void dispose() {
      do titleString.dispose();
      do instructionString.dispose();
      do startString.dispose();
      do board.dispose();

      do Memory.deAlloc(this);
      return;
   }
}
