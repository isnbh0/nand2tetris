// The 2048 game board consisting of nrows x ncols blocks.

/** Implements a graphical board. */
class Board {
   field int nrows, ncols;
   field int rOffset, cOffset;  // Parameters for drawing the board.
   field Array grid;

   /** Constructs a new board with a given number of rows and columns. */
   constructor Board new(int Arows, int Acols) {
      var int i, j;
      var Array row;

      let nrows = Arows;
      let ncols = Acols;
      let grid = Array.new(nrows);

      // Initialize grid with Blocks.
      let i = 0;
      while (i < nrows) {
         let row = Array.new(ncols);
         let j = 0;
         while (j < ncols) {
            let grid[i] = row;
            let row[j] = Block.new();
            let j = j + 1;
         }
         let i = i + 1;
      }

      let rOffset = 5;
      let cOffset = 17;
      return this;
   }

   method int getNrows() {
      return nrows;
   }

   method int getNcols() {
      return ncols;
   }

   /** Get Block at row r, column c. */
   method Block getBlockAt(int r, int c) {
      var Array row;
      var Block block;

      let row = grid[r];
      let block = row[c];
      return block;
   }

   /** Set value v to Block at position r, c. */
   method void setBlock(int v, int r, int c) {
      var Block block;
      let block = getBlockAt(r, c);
      do block.setValue(v);
      return;
   }

   /** Populate board.
       (baseline deterministic method -- try each Block from (0,0).)
       Not used. */
   method void populate() {
      var Block currBlock;
      var int r, c;
      var boolean done;

      let r = 0;
      let done = false;
      while (r < 4) {
         let c = 0;
         while (c < 4) {
            let currBlock = getBlockAt(r, c);
            if ((currBlock.isEmpty()) & ~done) {
               do setBlock(2, r, c);
               let done = true;
            }
            let c = c + 1;
         }
         let r = r + 1;
      }
      return;
   }

   /** Populate board, pseudo-randomly. */
   method boolean populateRNG() {
      var Block currBlock;
      var int r, c, d;
      var boolean done;

      let done = false;
      while (~done) {
         let r = Random.rand();
         let d = Random.rand() / 100;
         while (d > 0) {  // "convolution" for good randomness mod 4
            do Random.setSeed(Random.rand());
            let d = d - 1;
         }
         let c = Random.rand();
         let r = r - (r / nrows * nrows);
         let c = c - (c / ncols * ncols);

         let currBlock = getBlockAt(r, c);
         if (currBlock.isEmpty()) {
            do setBlock(2, r, c);
            let done = true;
         }
         else {
            if (isFull()) {
               return false;
            }
         }
      }
      return done;
   }

   /** Returns true if board is currently full. */
   method boolean isFull() {
      var boolean full;
      var int r, c;
      var Block currBlock;

      let full = true;
      let r = 0;
      while (r < nrows) {
         let c = 0;
         while (c < ncols) {
            let currBlock = getBlockAt(r, c);
            if (currBlock.isEmpty()) {
               let full = false;
               let c = ncols;
               let r = nrows;
            }
            let c = c + 1;
         }
         let r = r + 1;
      }
      return full;
   }

   /** Returns true if this board is same as other board. */
   method boolean sameAs(Board other) {
      var boolean same;
      var int r, c;
      var Block ourBlock, theirBlock;

      let same = true;
      let r = 0;
      while (r < nrows) {
         let c = 0;
         while (c < ncols) {
            let ourBlock = getBlockAt(r, c);
            let theirBlock = other.getBlockAt(r, c);
            if (~(ourBlock.getValue() = theirBlock.getValue())) {
               let same = false;
               let c = ncols;
               let r = nrows;
            }
            let c = c + 1;
         }
         let r = r + 1;
      }
      return same;
   }

   /** Returns true if board contains value higher than threshold. */
   method boolean containsBiggerThan(int threshold) {
      var int r, c;
      var boolean contains;
      var Block block;

      let contains = false;
      let r = 0;
      while (r < nrows) {
         let c = 0;
         while (c < ncols) {
            let block = getBlockAt(r, c);
            if (block.getValue() > threshold) {
               let contains = true;
               let c = ncols;
               let r = nrows;
            }
            let c = c + 1;
         }
         let r = r + 1;
      }
      return contains;
   }

   /** Initialize board. */
   method void initialize() {
      do populateRNG();
      do print();
      return;
   }

   method boolean isEmptyAt(int r, int c) {
      var Block block;
      let block = getBlockAt(r, c);
      return block.isEmpty();
   }

   /** Dispose this board. */
   method void dispose() {
      var Array row;
      var Block block;
      var int r, c;

      let r = 0;
      while (r < nrows) {
         let row = grid[r];
         let c = 0;
         while (c < ncols) {
            let block = row[c];
            do block.dispose();
            let c = c + 1;
         }
         do row.dispose();
         let r = r + 1;
      }
      do grid.dispose();

      do Memory.deAlloc(this);
      return;
   }

   /** Print a blank space c times.
       Helper for Board.print() below. */
   function void printOffset(int c) {
      var String space;
      var int ci;

      let space = " ";
      let ci = 0;
      while (ci < c) {
         do Output.printString(space);
         let ci = ci + 1;
      }

      do space.dispose();
      return;
   }

   /** Draw the board on the screen. */
   method void print() {
      // TODO: separate board printing logic (one-time) from number printing logic (every move)
      var int r, c, p, CHAR_WIDTH;
      var Block currBlock;
      var String wall, leftPillar, centerPillar;

      let leftPillar = "| ";
      let centerPillar = " | ";

      // Set starting point for cursor
      do Output.moveCursor(rOffset, 0);
      do Board.printOffset(cOffset);
      
      let CHAR_WIDTH = (Block.getMaxLength() + 3) * ncols + 1;
      let wall = String.new(CHAR_WIDTH);

      // Make hyphen wall for separating rows
      let p = 0;
      while (p < CHAR_WIDTH) {
         do wall.appendChar(45);  // "-"
         let p = p + 1;
      }

      let r = 0;
      while (r < nrows) {
         do Output.printString(wall);
         do Output.println();      // New line
         do Output.println();      // New line
         do Board.printOffset(cOffset);
         do Output.printString(leftPillar);
         let c = 0;
         while (c < ncols) {
            let currBlock = getBlockAt(r, c);
            do currBlock.print();
            do Output.printString(centerPillar);
            let c = c + 1;
         }
         do Output.println();      // New line
         do Output.println();      // New line
         do Board.printOffset(cOffset);

         let r = r + 1;
      }
      do Output.printString(wall);
      do Output.println();      // New line
      do Board.printOffset(cOffset);

      do leftPillar.dispose();
      do centerPillar.dispose();
      do wall.dispose();
      return;
   }

}
