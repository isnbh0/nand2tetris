// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        // do Array.init();
        do Math.init();
        // do String.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();

        do Main.main();

        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while (true) {
            do Sys.wait(5000);
        }
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int factor, i, j;

        let factor = 400;
        let i = 0;
        while (~(i = factor)) {
            let j = 0;
            while (~(j = duration)) {
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        var String err;

        let err = "ERR";
        do Output.printString(err);
        do Output.printInt(errorCode);

        do Sys.halt();
        return;
    }
}
